// Copyright (C) 2025 Ryan Daum <ryan.daum@gmail.com> This program is free
// software: you can redistribute it and/or modify it under the terms of the GNU
// General Public License as published by the Free Software Foundation, version
// 3.
//
// This program is distributed in the hope that it will be useful, but WITHOUT
// ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
// FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License along with
// this program. If not, see <https://www.gnu.org/licenses/>.
//

file = _{
    SOI ~
    skip ~
    (block ~ skip)* ~
    EOI
}

block = _{ 
    change_player |
    eval |
    cmd
}

skip = _{ (
    (" "* ~ NEWLINE) |
    "//" ~ (!NEWLINE ~ ANY)* ~ eol
)* }

change_player = _{ "@" ~ change_player_name ~ eol }
change_player_name = { ASCII_ALPHA_LOWER+ }

eval = { ";" ~ test }
cmd = { "%" ~ test }
test = _{ " "* ~ test_input ~ skip ~ expect? }
test_input = _{ test_line ~ eol ~ (">" ~ test_line ~ eol)* }
test_line = { (!NEWLINE ~ ANY)* }

expect = _{ expect_verbatim | expect_eval }
expect_verbatim = _{ "=" ~ expect_verbatim_line ~ eol }
expect_verbatim_line = { (!NEWLINE ~ ANY)* }

expect_eval = _{ ("<" | !(";" | "@" | "%" | "//")) ~ " "* ~ expect_eval_line ~ eol }
expect_eval_line = { (!NEWLINE ~ ANY)+ }

eol = _{ NEWLINE | EOI }
